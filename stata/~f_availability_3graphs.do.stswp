/*******************************************************************************
    Project: icfes referrals 
    Author: Reha Tuncer
    Date: 30.07.2025
    Description: figure network composition by SES - 3 graphs 
*******************************************************************************/
global dpath "/Users/reha.tuncer/Documents/GitHub/icfes-referrals/stata/"
global fpath "/Users/reha.tuncer/Documents/GitHub/icfes-referrals/figures/"
set scheme s2color, permanently

clear all

// Initialize globals for storing results
foreach own in low middle high {
    foreach other in low middle high {
        global prop_`own'_`other' = ""
        global se_`own'_`other' = ""
        global lb_`own'_`other' = ""
        global ub_`own'_`other' = ""
        global n_`own' = ""
    }
}

use "${dpath}dataset_z.dta", clear

// Calculate proportions for each SES group
foreach i in 1 2 3 {
    local ses_name = cond(`i'==1, "low", cond(`i'==2, "middle", "high"))
    
    preserve
    keep if own_estrato == `i'
    bysort own_id: egen p_low = mean(other_estrato == 1)
    bysort own_id: egen p_middle = mean(other_estrato == 2) 
    bysort own_id: egen p_high = mean(other_estrato == 3)
    bysort own_id: keep if _n == 1
    
    foreach j in low middle high {
        ci means p_`j'
        global prop_`ses_name'_`j' = r(mean)
        global se_`ses_name'_`j' = r(se)
        global lb_`ses_name'_`j' = r(lb)
        global ub_`ses_name'_`j' = r(ub)
        global n_`ses_name' = r(N)
    }
    restore
}

// Calculate university-level proportions
preserve
bysort other_id: gen counter = _n
keep if counter == 1
proportion other_estrato
matrix props_A = r(table)
global prop_A_low = props_A[1,1]
global prop_A_middle = props_A[1,2]
global prop_A_high = props_A[1,3]
restore

// Statistical tests (optional - can be commented out if not needed)
foreach own in low middle high {
    display "=========================================="
    display upper("`own'") "-SES REFERRERS: NETWORK vs UNIVERSITY"
    display "=========================================="
    foreach other in low middle high {
        display "  `other' recipients:"
        ttesti ${n_`own'} ${prop_`own'_`other'} ${se_`own'_`other'} ${prop_A_`other'}
    }
    display ""
}

// Create data for plotting
clear
set obs 3
gen xpos = _n

// Variables for all three plots
gen prop_to_low = .
gen prop_to_middle = .
gen prop_to_high = .
gen ci_lower_low = .
gen ci_upper_low = .
gen ci_lower_middle = .
gen ci_upper_middle = .
gen ci_lower_high = .
gen ci_upper_high = .

// Fill in data for connections TO LOW-SES
replace prop_to_low = ${prop_low_low} if xpos == 1
replace prop_to_low = ${prop_middle_low} if xpos == 2
replace prop_to_low = ${prop_high_low} if xpos == 3
replace ci_lower_low = ${lb_low_low} if xpos == 1
replace ci_lower_low = ${lb_middle_low} if xpos == 2
replace ci_lower_low = ${lb_high_low} if xpos == 3
replace ci_upper_low = ${ub_low_low} if xpos == 1
replace ci_upper_low = ${ub_middle_low} if xpos == 2
replace ci_upper_low = ${ub_high_low} if xpos == 3

// Fill in data for connections TO MIDDLE-SES
replace prop_to_middle = ${prop_low_middle} if xpos == 1
replace prop_to_middle = ${prop_middle_middle} if xpos == 2
replace prop_to_middle = ${prop_high_middle} if xpos == 3
replace ci_lower_middle = ${lb_low_middle} if xpos == 1
replace ci_lower_middle = ${lb_middle_middle} if xpos == 2
replace ci_lower_middle = ${lb_high_middle} if xpos == 3
replace ci_upper_middle = ${ub_low_middle} if xpos == 1
replace ci_upper_middle = ${ub_middle_middle} if xpos == 2
replace ci_upper_middle = ${ub_high_middle} if xpos == 3

// Fill in data for connections TO HIGH-SES
replace prop_to_high = ${prop_low_high} if xpos == 1
replace prop_to_high = ${prop_middle_high} if xpos == 2
replace prop_to_high = ${prop_high_high} if xpos == 3
replace ci_lower_high = ${lb_low_high} if xpos == 1
replace ci_lower_high = ${lb_middle_high} if xpos == 2
replace ci_lower_high = ${lb_high_high} if xpos == 3
replace ci_upper_high = ${ub_low_high} if xpos == 1
replace ci_upper_high = ${ub_middle_high} if xpos == 2
replace ci_upper_high = ${ub_high_high} if xpos == 3

// Plot 1: Connections TO LOW-SES recipients
twoway (bar prop_to_low xpos, barw(0.6) fcolor(gs11) lcolor(gs4)) ///
       (function y=${prop_A_low}, range(0.5 3.5) lpattern(dash) lcolor(dknavy) lwidth(medthick)) ///
       (rcap ci_upper_low ci_lower_low xpos, color(gs4) lwidth(medium)) ///
       , ///
       xlabel(1 "Low" 2 "Middle" 3 "High", labsize(medium)) ///
       ylabel(0(0.1)0.8, angle(0) gmin gmax labsize(medium)) ///
       ytitle("Proportion", size(medium)) ///
       xtitle("Referrer SES") ///
	   title("Low-SES peers in network", size(medlarge)) ///
       legend(order(2 "University share") ring(0) pos(12) region(lcolor(none)) size(medium)) ///
       graphregion(color(white)) bgcolor(white) ///
       xscale(range(0.5 3.5)) xsize(1) ysize(1) ///
       name(to_low_ses, replace)  
graph export "${fpath}connections_to_low_ses.png", name(to_low_ses) replace

// Plot 2: Connections TO MIDDLE-SES recipients
twoway (bar prop_to_middle xpos, barw(0.6) fcolor(gs11) lcolor(gs4)) ///
       (function y=${prop_A_middle}, range(0.5 3.5) lpattern(dash) lcolor(orange) lwidth(medthick)) ///
       (rcap ci_upper_middle ci_lower_middle xpos, color(gs4) lwidth(medium)) ///
       , ///
       xlabel(1 "Low" 2 "Middle" 3 "High", labsize(medium)) ///
       ylabel(0(0.1)0.8, angle(0) gmin gmax labsize(medium)) ///
       xtitle("Referrer SES") ///
	   ytitle("Proportion", size(medium)) ///
       title("Middle-SES peers in network", size(medlarge)) ///
       legend(order(2 "University share") ring(0) pos(12) region(lcolor(none)) size(medium)) ///
       graphregion(color(white)) bgcolor(white) ///
       xscale(range(0.5 3.5)) xsize(1) ysize(1) ///
       name(to_middle_ses, replace) 
graph export "${fpath}connections_to_middle_ses.png", name(to_middle_ses) replace  
// Plot 3: Connections TO HIGH-SES recipients
twoway (bar prop_to_high xpos, barw(0.6) fcolor(gs11) lcolor(gs4)) ///
       (function y=${prop_A_high}, range(0.5 3.5) lpattern(dash) lcolor(red) lwidth(medthick)) ///
       (rcap ci_upper_high ci_lower_high xpos, color(gs4) lwidth(medium)) ///
       , ///
       xlabel(1 "Low" 2 "Middle" 3 "High", labsize(medium)) ///
       ylabel(0(0.1)0.8, angle(0) gmin gmax labsize(medium)) ///
       ytitle("Proportion", size(medium)) ///
	   xtitle("Referrer SES") ///
       title("High-SES peers in network", size(medlarge)) ///
       legend(order(2 "University share") ring(0) pos(12) region(lcolor(none)) size(medium)) ///
       graphregion(color(white)) bgcolor(white) ///
       xscale(range(0.5 3.5)) xsize(1) ysize(1) ///
       name(to_high_ses, replace) 
graph export "${fpath}connections_to_high_ses.png", name(to_high_ses) replace

// Combine all three plots
graph combine to_low_ses to_middle_ses to_high_ses, ///
    rows(1) cols(3) ///
    title("")  ///
    b1title("Referrer SES", size(medium))  ///
	graphregion(color(white) margin(0))
graph export "${fpath}combined_ses_network_composition.png", replace
// Export individual plots



// Export combined plot

