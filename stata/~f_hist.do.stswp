/*******************************************************************************
    Project: icfes referrals 
    Author: Reha Tuncer
    Date: 18.03.2025
    Description: figures reading/math combined
*******************************************************************************/
global dpath "/Users/reha.tuncer/Documents/GitHub/icfes-referrals/stata/"
global fpath "/Users/reha.tuncer/Documents/GitHub/icfes-referrals/figures/"
set scheme s2color, permanently

	

use "${dpath}dataset_z.dta", clear
keep if nomination
keep own_id tie other_score
collapse (mean) tie other_score , by(own_id)
rename tie tie_R
rename other_score other_score_R
save "${dpath}temp.dta", replace

clear all
use "${dpath}dataset_z.dta", clear
collapse (mean) tie other_score , by(own_id)
rename tie tie_N
rename other_score other_score_N
merge 1:1 own_id using "${dpath}temp.dta"
	
preserve	
foreach var in _R _N {
	egen med`var' = median(tie`var')
    egen lqt`var' = pctile(tie`var'), p(25)
    egen uqt`var' = pctile(tie`var'), p(75)
    egen iqr`var' = iqr(tie`var')
    egen mean`var' = mean(tie`var')
    gen l`var' = tie`var' if(tie`var' >= lqt`var'-1.5*iqr`var')
    egen ls`var' = min(l`var')
    gen u`var' =tie`var' if(tie`var' <= uqt`var'+1.5*iqr`var')
    egen us`var' = max(u`var')
}
gen ypos2 = -1.5
gen ypos = -3


// draw
sum tie_R
local min1 = r(min)
local max1 = r(max)
local n1 = r(N)
sum tie_N
local min2 = r(min)
local max2 = r(max)
local n2 = r(N)
local min = min(`min1',`min2')
local max = max(`max1',`max2')
local n = min(`n1',`n2')
local bins = min(sqrt(`n'),10*ln(`n')/ln(10))
local width = (`max'-`min')/`bins'
    
twoway  (histogram tie_N, percent start(`min') width(`width') fcolor(orange%60) lcolor(gs4) lwidth(thin)) ///
(histogram tie_R, percent start(`min') width(`width') fcolor(dknavy%60) lcolor(gs4) lwidth(thin)) ///
       || rbar lqt_R uqt_R ypos, horiz fcolor(dknavy*.7) lcolor(gs4) barw(1.2) ///
       || rbar med_R uqt_R ypos, horiz fcolor(dknavy*.7) lcolor(gs4) barw(1.2) ///
       || rspike lqt_R ls_R ypos, horiz lcolor(gs4) ///
       || rspike uqt_R us_R ypos, horiz lcolor(gs4) ///
       || rcap ls_R ls_R ypos, horiz msize(*1) lcolor(gs4) ///
       || rcap us_R us_R ypos, horiz msize(*1) lcolor(gs4) ///
       || scatter ypos mean_R, msymbol(o) msize(*.5) fcolor(gs4) mcolor(gs4) ///
       || rbar lqt_N uqt_N ypos2, horiz fcolor(orange*.7) lcolor(gs4) barw(1.2) ///
       || rbar med_N uqt_N ypos2, horiz fcolor(orange*.7) lcolor(gs4) barw(1.2) ///
       || rspike lqt_N ls_N ypos2, horiz lcolor(gs4) ///
       || rspike uqt_N us_N ypos2, horiz lcolor(gs4) ///
       || rcap ls_N ls_N ypos2, horiz msize(*1) lcolor(gs4) ///
       || rcap us_N us_N ypos2, horiz msize(*1) lcolor(gs4) ///
       || scatter ypos2 mean_N, msymbol(o) msize(*.5) fcolor(gs4) mcolor(gs4) ///
       , xlabel(0(10)50) ///
         ylabel(0(10)50, gmax angle(0)) ///
         ytitle("Percent") ///
         xtitle("Nb. courses taken together ") ///
         legend(order(1 "Network" 2 "Referral") ring(0) pos(12) rows(1) region(lcolor(none))) ///
         title("Average courses taken for network and referrals") ///
         graphregion(color(white)) bgcolor(white) ///
         name(tie_hist, replace)
		  
    graph export "${fpath}tie_hist.png", replace
restore
	
preserve	
foreach var in _R _N {
	egen med`var' = median(other_score`var')
    egen lqt`var' = pctile(other_score`var'), p(25)
    egen uqt`var' = pctile(other_score`var'), p(75)
    egen iqr`var' = iqr(other_score`var')
    egen mean`var' = mean(other_score`var')
    gen l`var' = other_score`var' if(other_score`var' >= lqt`var'-1.5*iqr`var')
    egen ls`var' = min(l`var')
    gen u`var' = other_score`var' if(other_score`var' <= uqt`var'+1.5*iqr`var')
    egen us`var' = max(u`var')
}
gen ypos2 = -1.5
gen ypos = -3
// draw
sum other_score_R
local min1 = r(min)
local max1 = r(max)
local n1 = r(N)
sum other_score_N
local min2 = r(min)
local max2 = r(max)
local n2 = r(N)
local min = min(`min1',`min2')
local max = max(`max1',`max2')
local n = min(`n1',`n2')
local bins = min(sqrt(`n'),10*ln(`n')/ln(10))
local width = (`max'-`min')/`bins'
    
twoway  (histogram other_score_N, percent start(`min') width(`width') fcolor(orange%60) lcolor(gs4) lwidth(thin)) ///
(histogram other_score_R, percent start(`min') width(`width') fcolor(dknavy%60) lcolor(gs4) lwidth(thin)) ///
       || rbar lqt_R uqt_R ypos, horiz fcolor(dknavy*.7) lcolor(gs4) barw(1.2) ///
       || rbar med_R uqt_R ypos, horiz fcolor(dknavy*.7) lcolor(gs4) barw(1.2) ///
       || rspike lqt_R ls_R ypos, horiz lcolor(gs4) ///
       || rspike uqt_R us_R ypos, horiz lcolor(gs4) ///
       || rcap ls_R ls_R ypos, horiz msize(*1) lcolor(gs4) ///
       || rcap us_R us_R ypos, horiz msize(*1) lcolor(gs4) ///
       || scatter ypos mean_R, msymbol(o) msize(*.5) fcolor(gs4) mcolor(gs4) ///
       || rbar lqt_N uqt_N ypos2, horiz fcolor(orange*.7) lcolor(gs4) barw(1.2) ///
       || rbar med_N uqt_N ypos2, horiz fcolor(orange*.7) lcolor(gs4) barw(1.2) ///
       || rspike lqt_N ls_N ypos2, horiz lcolor(gs4) ///
       || rspike uqt_N us_N ypos2, horiz lcolor(gs4) ///
       || rcap ls_N ls_N ypos2, horiz msize(*1) lcolor(gs4) ///
       || rcap us_N us_N ypos2, horiz msize(*1) lcolor(gs4) ///
       || scatter ypos2 mean_N, msymbol(o) msize(*.5) fcolor(gs4) mcolor(gs4) ///
       , xlabel(20(20)100) ///
         ylabel(0(10)50, gmax angle(0)) ///
         ytitle("Percent") ///
         xtitle("Score") ///
         legend(order(1 "Network" 2 "Referral") ring(0) pos(12) rows(1) region(lcolor(none))) ///
         title("Average score of network and referrals") ///
         graphregion(color(white)) bgcolor(white) ///
         name(other_score_hist, replace)
		  
    graph export "${fpath}other_score_hist.png", replace
restore