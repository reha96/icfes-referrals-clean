/*******************************************************************************
    Project: icfes referrals 
    Author: Reha Tuncer
    Date: 18.03.2025
    Description: figure referral rates overlaid with pop share - ALL SES COMBINATIONS
*******************************************************************************/

global dpath "/Users/reha.tuncer/Documents/GitHub/icfes-referrals/stata/"
global fpath "/Users/reha.tuncer/Documents/GitHub/icfes-referrals/figures/"
set scheme s2color, permanently

clear all
use "${dpath}dataset_z.dta"

// Calculate proportions for all SES combinations and store in globals
foreach own in low middle high {
    foreach other in low middle high {
        global prop_`own'_`other' = ""
        global se_`own'_`other' = ""
		global sd_`own'_`other' = ""
        global prop_`own'_`other'2 = ""
        global se_`own'_`other'2 = ""
        global n_`own' = ""
        global n_`own'2 = ""
    }
}

// Network proportions (all connections)
foreach i in 1 2 3 {
    local ses_name = cond(`i'==1, "low", cond(`i'==2, "middle", "high"))
    
    preserve
    keep if own_estrato == `i'
    bysort own_id: egen p_low = mean(other_estrato == 1)
    bysort own_id: egen p_middle = mean(other_estrato == 2)  
    bysort own_id: egen p_high = mean(other_estrato == 3)
    bysort own_id: keep if _n == 1
    collapse (mean) prop_low=p_low prop_middle=p_middle prop_high=p_high ///
             (sem) se_low=p_low se_middle=p_middle se_high=p_high ///
			 (sd) sd_low=p_low sd_middle=p_middle sd_high=p_high ///
             (count) n=p_low
    
    global prop_`ses_name'_low2 = prop_low[1]
    global prop_`ses_name'_middle2 = prop_middle[1] 
    global prop_`ses_name'_high2 = prop_high[1]
    global se_`ses_name'_low2 = se_low[1]
    global se_`ses_name'_middle2 = se_middle[1]
    global se_`ses_name'_high2 = se_high[1]
	global sd_`ses_name'_low2 = sd_low[1]
    global sd_`ses_name'_middle2 = sd_middle[1]
    global sd_`ses_name'_high2 = sd_high[1]
    global n_`ses_name'2 = n[1]
    restore
}

// Referral proportions (nominations only)
foreach i in 1 2 3 {
    local ses_name = cond(`i'==1, "low", cond(`i'==2, "middle", "high"))
    
    preserve
    keep if own_estrato == `i' & nomination
    bysort own_id: egen p_low = mean(other_estrato == 1)
    bysort own_id: egen p_middle = mean(other_estrato == 2)
    bysort own_id: egen p_high = mean(other_estrato == 3)
    bysort own_id: keep if _n == 1
    collapse (mean) prop_low=p_low prop_middle=p_middle prop_high=p_high ///
             (sem) se_low=p_low se_middle=p_middle se_high=p_high ///
			 (sd) sd_low=p_low sd_middle=p_middle sd_high=p_high ///
             (count) n=p_low
    
    global prop_`ses_name'_low = prop_low[1]
    global prop_`ses_name'_middle = prop_middle[1]
    global prop_`ses_name'_high = prop_high[1] 
    global se_`ses_name'_low = se_low[1]
    global se_`ses_name'_middle = se_middle[1]
    global se_`ses_name'_high = se_high[1]
	global sd_`ses_name'_low = sd_low[1]
    global sd_`ses_name'_middle = sd_middle[1]
    global sd_`ses_name'_high = sd_high[1]
    global n_`ses_name' = n[1]
    restore
}

// Create visualization dataset
clear
set obs 9
gen own_ses = ceil(_n/3)
gen other_ses = mod(_n-1, 3) + 1
gen xpos = .
gen xpos2 = .

// Position bars
replace xpos = 1 - 0.2 if own_ses == 1 & other_ses == 1
replace xpos = 2 - 0.2 if own_ses == 1 & other_ses == 2  
replace xpos = 3 - 0.2 if own_ses == 1 & other_ses == 3
replace xpos = 4.5 - 0.2 if own_ses == 2 & other_ses == 1
replace xpos = 5.5 - 0.2 if own_ses == 2 & other_ses == 2
replace xpos = 6.5 - 0.2 if own_ses == 2 & other_ses == 3
replace xpos = 8 - 0.2 if own_ses == 3 & other_ses == 1
replace xpos = 9 - 0.2 if own_ses == 3 & other_ses == 2
replace xpos = 10 - 0.2 if own_ses == 3 & other_ses == 3
replace xpos2 = xpos + 0.4

// Fill proportions and standard errors
gen proportion = .
gen se = .
gen proportion2 = .
gen se2 = .

// Low SES
replace proportion = ${prop_low_low} if own_ses == 1 & other_ses == 1
replace proportion = ${prop_low_middle} if own_ses == 1 & other_ses == 2
replace proportion = ${prop_low_high} if own_ses == 1 & other_ses == 3
replace se = ${se_low_low} if own_ses == 1 & other_ses == 1
replace se = ${se_low_middle} if own_ses == 1 & other_ses == 2
replace se = ${se_low_high} if own_ses == 1 & other_ses == 3
replace proportion2 = ${prop_low_low2} if own_ses == 1 & other_ses == 1
replace proportion2 = ${prop_low_middle2} if own_ses == 1 & other_ses == 2
replace proportion2 = ${prop_low_high2} if own_ses == 1 & other_ses == 3
replace se2 = ${se_low_low2} if own_ses == 1 & other_ses == 1
replace se2 = ${se_low_middle2} if own_ses == 1 & other_ses == 2
replace se2 = ${se_low_high2} if own_ses == 1 & other_ses == 3

// Middle SES  
replace proportion = ${prop_middle_low} if own_ses == 2 & other_ses == 1
replace proportion = ${prop_middle_middle} if own_ses == 2 & other_ses == 2
replace proportion = ${prop_middle_high} if own_ses == 2 & other_ses == 3
replace se = ${se_middle_low} if own_ses == 2 & other_ses == 1
replace se = ${se_middle_middle} if own_ses == 2 & other_ses == 2
replace se = ${se_middle_high} if own_ses == 2 & other_ses == 3
replace proportion2 = ${prop_middle_low2} if own_ses == 2 & other_ses == 1
replace proportion2 = ${prop_middle_middle2} if own_ses == 2 & other_ses == 2
replace proportion2 = ${prop_middle_high2} if own_ses == 2 & other_ses == 3
replace se2 = ${se_middle_low2} if own_ses == 2 & other_ses == 1
replace se2 = ${se_middle_middle2} if own_ses == 2 & other_ses == 2
replace se2 = ${se_middle_high2} if own_ses == 2 & other_ses == 3

// High SES
replace proportion = ${prop_high_low} if own_ses == 3 & other_ses == 1
replace proportion = ${prop_high_middle} if own_ses == 3 & other_ses == 2
replace proportion = ${prop_high_high} if own_ses == 3 & other_ses == 3
replace se = ${se_high_low} if own_ses == 3 & other_ses == 1
replace se = ${se_high_middle} if own_ses == 3 & other_ses == 2
replace se = ${se_high_high} if own_ses == 3 & other_ses == 3
replace proportion2 = ${prop_high_low2} if own_ses == 3 & other_ses == 1
replace proportion2 = ${prop_high_middle2} if own_ses == 3 & other_ses == 2
replace proportion2 = ${prop_high_high2} if own_ses == 3 & other_ses == 3
replace se2 = ${se_high_low2} if own_ses == 3 & other_ses == 1
replace se2 = ${se_high_middle2} if own_ses == 3 & other_ses == 2
replace se2 = ${se_high_high2} if own_ses == 3 & other_ses == 3

// Convert to percentages and calculate confidence intervals
replace proportion = proportion * 100
replace se = se * 100  
replace proportion2 = proportion2 * 100
replace se2 = se2 * 100
gen ci_lower = proportion - 1.96*se
gen ci_upper = proportion + 1.96*se
gen ci_lower2 = proportion2 - 1.96*se2
gen ci_upper2 = proportion2 + 1.96*se2

// Create graph
twoway (bar proportion2 xpos, barw(0.4) fcolor(gs8) lcolor(gs4)) ///
       (rcap ci_lower2 ci_upper2 xpos, lcolor(gs4)) ///
       (bar proportion xpos2, barw(0.4) fcolor(gs14) lcolor(gs4)) ///
       (rcap ci_lower ci_upper xpos2, lcolor(gs4)) ///
	   (pci 0 3.75 80 3.75, lwidth(thin) lpattern(dash) lcolor(black)) /// 
       (pci 0 7.25 80 7.25, lwidth(thin) lpattern(dash) lcolor(black)) /// 
       , ///
       xlabel(1 "Low" 2 "Middle" 3 "High" 4.5 "Low" 5.5 "Middle" 6.5 "High" 8 "Low" 9 "Middle" 10 "High", angle(0)) ///
       ylabel(0(10)80, angle(0) format(%9.0f) gmin gmax) ///
       ytitle("Percent") ///
       subtitle("     Low-SES                     Middle-SES                     High-SES     ") ///
	   xtitle("") ///
       title("Referral rates compared to network shares") ///
       legend(order(1 "Network" 3 "Referral") ///
              ring(1) pos(12) rows(1) region(lcolor(none)) ) ///
       graphregion(color(white)) bgcolor(white) ///
       xscale(range(0.5 10.5)) ///
       name(referral_rates, replace)

graph export "${fpath}all_ses_referral_rates.png", replace

// =============================================================================
// STATISTICAL TESTS USING TTESTI

cls

display "LOW-SES REFERRERS: REFERRALS VS NETWORK"
ttesti ${n_low2} ${prop_low_low2} ${sd_low_low2} ${n_low} ${prop_low_low} ${sd_low_low}, unequal
ttesti ${n_low2} ${prop_low_middle2} ${sd_low_middle2} ${n_low} ${prop_low_middle} ${sd_low_middle}, unequal
ttesti ${n_low2} ${prop_low_high2} ${sd_low_high2} ${n_low} ${prop_low_high} ${sd_low_high}, unequal

display "MIDDLE-SES REFERRERS:"
ttesti ${n_middle2} ${prop_middle_low2} ${sd_middle_low2} ${n_middle} ${prop_middle_low} ${sd_middle_low}, unequal
ttesti ${n_middle2} ${prop_middle_middle2} ${sd_middle_middle2} ${n_middle} ${prop_middle_middle} ${sd_middle_middle}, unequal
ttesti ${n_middle2} ${prop_middle_high2} ${sd_middle_high2} ${n_middle} ${prop_middle_high} ${sd_middle_high}, unequal

display "HIGH-SES REFERRERS:"
ttesti ${n_high2} ${prop_high_low2} ${sd_high_low2} ${n_high} ${prop_high_low} ${sd_high_low}, unequal
ttesti ${n_high2} ${prop_high_middle2} ${sd_high_middle2} ${n_high} ${prop_high_middle} ${sd_high_middle}, unequal
ttesti ${n_high2} ${prop_high_high2} ${sd_high_high2} ${n_high} ${prop_high_high} ${sd_high_high}, unequal
