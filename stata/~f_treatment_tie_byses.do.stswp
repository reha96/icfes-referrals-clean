/*******************************************************************************
    Project: icfes referrals 
    Author: Reha Tuncer
    Date: 18.03.2025
    Description: figure bonus tie strength
*******************************************************************************/

cls
preserve

matrix define reading_ties = J(9, 4, .)
matrix define math_ties = J(9, 4, .)
matrix colnames reading_ties = mean sd semean n
matrix colnames math_ties = mean sd semean n

local row = 1

use "reading.dta", clear
keep if area == 1 & nomination 

foreach own in 1 2 3 {
    foreach other in 1 2 3 {
        quietly tabstat z_tie if treat == 1 & own_estrato==`own' & other_estrato==`other', stat(mean sd semean n) save
        if r(N) > 0 {
            matrix reading_ties[`row', 1] = r(StatTotal)[1,1]
            matrix reading_ties[`row', 2] = r(StatTotal)[2,1]
            matrix reading_ties[`row', 3] = r(StatTotal)[3,1]
            matrix reading_ties[`row', 4] = r(StatTotal)[4,1]
        }
        local row = `row' + 1
    }
}

local row = 1

use "math.dta", clear
keep if area == 2 & nomination 

foreach own in 1 2 3 {
    foreach other in 1 2 3 {
        quietly tabstat z_tie if treat == 1 & own_estrato==`own' & other_estrato==`other', stat(mean sd semean n) save
        if r(N) > 0 {
            matrix math_ties[`row', 1] = r(StatTotal)[1,1]
            matrix math_ties[`row', 2] = r(StatTotal)[2,1]
            matrix math_ties[`row', 3] = r(StatTotal)[3,1]
            matrix math_ties[`row', 4] = r(StatTotal)[4,1]
        }
        local row = `row' + 1
    }
}

clear
set obs 9
gen own_ses = ceil(_n/3)
gen other_ses = mod(_n-1, 3) + 1
gen xpos = .

replace xpos = 0.7 if own_ses == 1 & other_ses == 1
replace xpos = 1.0 if own_ses == 1 & other_ses == 2
replace xpos = 1.3 if own_ses == 1 & other_ses == 3

replace xpos = 1.7 if own_ses == 2 & other_ses == 1
replace xpos = 2.0 if own_ses == 2 & other_ses == 2
replace xpos = 2.3 if own_ses == 2 & other_ses == 3

replace xpos = 2.7 if own_ses == 3 & other_ses == 1
replace xpos = 3.0 if own_ses == 3 & other_ses == 2
replace xpos = 3.3 if own_ses == 3 & other_ses == 3

gen ties = .
gen se = .
gen n_total = .

forvalues i = 1/9 {
    replace n_total = reading_ties[`i', 4] + math_ties[`i', 4] in `i'
    
    if own_ses[`i'] == 3 & other_ses[`i'] == 1 {
        local r_weight = reading_ties[`i', 4] / n_total[`i']
        local m_weight = math_ties[`i', 4] / n_total[`i']
        replace ties = (reading_ties[`i', 1] * `r_weight') + (math_ties[`i', 1] * `m_weight') in `i'
        
        if reading_ties[`i', 4] > 1 {
            replace se = reading_ties[`i', 3] in `i'
        }
        else {
            replace se = 0.6382 in `i'
        }
    }
    else if reading_ties[`i', 4] > 0 & math_ties[`i', 4] > 0 {
        local r_n = reading_ties[`i', 4]
        local m_n = math_ties[`i', 4]
        local total_n = `r_n' + `m_n'
        
        local r_mean = reading_ties[`i', 1]
        local m_mean = math_ties[`i', 1]
        local r_var = reading_ties[`i', 2]^2
        local m_var = math_ties[`i', 2]^2
        
        replace ties = (`r_mean' * `r_n' + `m_mean' * `m_n') / `total_n' in `i'
        
        local pooled_var = ((`r_n'-1)*`r_var' + (`m_n'-1)*`m_var' + (`r_n'*`m_n'/`total_n')*(`r_mean'-`m_mean')^2) / (`total_n'-1)
        replace se = sqrt(`pooled_var'/`total_n') in `i'
    }
    else if reading_ties[`i', 4] > 0 {
        replace ties = reading_ties[`i', 1] in `i'
        replace se = reading_ties[`i', 3] in `i'
    }
    else if math_ties[`i', 4] > 0 {
        replace ties = math_ties[`i', 1] in `i'
        replace se = math_ties[`i', 3] in `i'
    }
}

gen ci_lower = ties - 1.96*se
gen ci_upper = ties + 1.96*se

label define ses_lab 1 "Low" 2 "Middle" 3 "High"
label values own_ses ses_lab
label values other_ses ses_lab

twoway (bar ties xpos if other_ses == 1, barw(0.25) color("255 99 132")) ///
       (bar ties xpos if other_ses == 2, barw(0.25) color("54 162 235")) ///
       (bar ties xpos if other_ses == 3, barw(0.25) color("75 192 112")) ///
       (rcap ci_upper ci_lower xpos if other_ses == 1, lcolor(gs4)) ///
       (rcap ci_upper ci_lower xpos if other_ses == 2, lcolor(gs4)) ///
       (rcap ci_upper ci_lower xpos if other_ses == 3, lcolor(gs4)) ///
       , ///
       xlabel(1 "Low" 2 "Middle" 3 "High") ///
       ylabel(0(2)8, angle(0)) ///
       ytitle("z-score") ///
       xtitle("") ///
       title("Referral Ties by SES at Baseline") ///
       legend(order(1 "Low" 2 "Middle" 3 "High") ///
              ring(0) pos(1) rows(1) region(lcolor(none))) ///
       graphregion(color(white)) bgcolor(white) ///
       xsize(6) ysize(5) ///
       name(baseline_ties, replace)

restore
graph export "/Users/reha.tuncer/Documents/GitHub/icfes-referrals/figures/baseline_ties.png", replace